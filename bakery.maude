mod BAKERY is
    *** Imports
    including CONFIGURATION .
    protecting NAT .

    *** Sorts
    sorts GBState Mode .
    subsort Nat < Oid .

    *** Operations
    op [[_]] : Configuration -> GBState .
    ops sleep wait crit : -> Mode [ctor] .
    op initial : Nat -> GBState .
    op auxInitial : Nat -> Configuration .

    *** Classes
    --- class BProcess | mode: Mode, number: Nat .
    sort BProcess .
    subsort BProcess < Cid .
    op BProcess : -> BProcess .
    op mode:_ : Mode -> Attribute [gather (&)] .
    op number:_ : Nat -> Attribute [gather (&)] .

    --- class Dispenser | next: Nat, last: Nat .
    sort Dispenser .
    subsort Dispenser < Cid .
    op Dispenser : -> Dispenser .
    op next:_ : Nat -> Attribute [gather (&)] .
    op last:_ : Nat -> Attribute [gather (&)] .

    *** Vars
    vars N L : Nat .
    vars dispenserId processId : Oid .
    var conf : Configuration .

    *** Equations
    eq initial (N) = [[< 0 : Dispenser | next: 1 , last: 1 > auxInitial(N) ]] .
    eq auxInitial (0) = < 0 : BProcess | mode: sleep, number: 0 > .
    eq auxInitial (s N) = < N : BProcess | mode: sleep, number: 0 > auxInitial(N) .

    *** Rules
    rl [take-number] : 
    [[ < dispenserId : Dispenser | next: N, last: L > < processId : BProcess | mode: sleep, number: 0 > conf ]] 
    => 
     [[ < dispenserId : Dispenser | next: N, last: s L > < processId : BProcess | mode: wait, number: L > conf ]] .

    rl [get-atended] : 
    [[ < dispenserId : Dispenser | next: N, last: L > < processId : BProcess | mode: wait, number: N > conf ]] 
    => 
    [[ < dispenserId : Dispenser | next: N, last: L > < processId : BProcess | mode: crit, number: N > conf ]]  .

    rl [leave] : 
    [[ < dispenserId : Dispenser | next: N, last: L > < processId : BProcess | mode: crit, number: N > conf ]]
    => 
    [[ < dispenserId : Dispenser | next: s N, last: L > < processId : BProcess | mode: sleep, number: 0 > conf ]]  .
    
endm
